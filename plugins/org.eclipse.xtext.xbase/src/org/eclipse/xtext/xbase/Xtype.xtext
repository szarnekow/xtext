/*******************************************************************************
 * Copyright (c) 2010 itemis AG (http://www.itemis.eu) and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *******************************************************************************/
 
grammar org.eclipse.xtext.xbase.Xtype with org.eclipse.xtext.common.Terminals

import "http://www.eclipse.org/xtext/xbase/Xtype"
import "http://www.eclipse.org/xtext/common/JavaVMTypes"

JvmTypeReference:
    JvmParameterizedTypeReference =>({JvmGenericArrayTypeReference.componentType=current} '['']')* 
  | XFunctionTypeRef;

XFunctionTypeRef:
  ('(' (paramTypes+=JvmTypeReference (',' paramTypes+=JvmTypeReference)*)? ')')? '=>' returnType=JvmTypeReference;

JvmParameterizedTypeReference:
  type=[JvmType|QualifiedName] (=>'<' arguments+=JvmArgumentTypeReference (',' arguments+=JvmArgumentTypeReference)* '>')?;

JvmArgumentTypeReference returns JvmTypeReference:
	JvmTypeReference | JvmWildcardTypeReference;

JvmWildcardTypeReference:
  {JvmWildcardTypeReference} '?' (constraints+=JvmUpperBound | constraints+=JvmLowerBound)?;
  
JvmUpperBound : 
 'extends' typeReference=JvmTypeReference;

JvmUpperBoundAnded returns JvmUpperBound: 
 '&' typeReference=JvmTypeReference;

JvmLowerBound :
 'super' typeReference=JvmTypeReference;

JvmTypeParameter :
  name=ValidID 
  ((constraints+=JvmUpperBound (constraints+=JvmUpperBoundAnded)*) |
   (constraints+=JvmLowerBound))?;

QualifiedName:
  ValidID ('.' ValidID)*;
  
ValidID:
	ID
;

terminal ID:
	'^'? ('a'..'z'|'A'..'Z'|'$'|'_') ('a'..'z'|'A'..'Z'|'$'|'_'|'0'..'9')*;